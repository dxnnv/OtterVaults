plugins {
    id 'com.gradleup.shadow' version '8.3.5'
    id 'java'
}

group = 'dev.dxnny'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly("com.h2database:h2:2.3.232")
    compileOnly ("org.projectlombok:lombok:1.18.34")

    implementation("com.zaxxer:HikariCP:6.3.0")
    implementation("dev.dejvokep:boosted-yaml:1.3.6")
    implementation("co.aikar:acf-paper:0.5.1-SNAPSHOT")

    annotationProcessor("org.projectlombok:lombok:1.18.34")
}

def targetJavaVersion = 21
def javaVersion = JavaVersion.toVersion(targetJavaVersion)

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "OtterVaults-${version}.jar"

    relocate 'dev.dejvokep.boostedyaml', 'dev.dxnny.ottervaults.libs'
    relocate 'co.aikar.commands', 'dev.dxnny.acf'
    relocate 'co.aikar.locales', 'dev.dxnny.locales'

    exclude "org/intellij/lang/annotations/**"
    exclude "org/jetbrains/annotations/"

    destinationDirectory.set(project.file("jars"))
}

tasks.build {
    dependsOn('shadowJar')
}